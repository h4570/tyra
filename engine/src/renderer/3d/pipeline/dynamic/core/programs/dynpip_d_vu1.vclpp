; _____        ____   ___
;   |     \/   ____| |___|
;   |     |   |   \  |   |
;---------------------------------------------------------------
; Copyright 2022, tyra - https://github.com/h4570/tyra
; Licensed under Apache License 2.0
; Sandro Sobczy≈Ñski <sandro.sobczynski@gmail.com>
;
;---------------------------------------------------------------
; Triangle list
; Cull = Standard PS2 way. clipw polys are culled.
; Animation
; Directional lights
;---------------------------------------------------------------

.syntax new
.name DynPipVU1_D
.vu
.init_vf_all
.init_vi_all

#include "src/renderer/3d/pipeline/shared/vcl_sml.i"
#include "src/renderer/3d/pipeline/shared/tyra_macros.i"
#include "inc/renderer/3d/pipeline/dynamic/core/programs/dynpip_vu1_shared_defines.h"

#define RGBA_STORE_OFFSET   0
#define XYZ2_STORE_OFFSET   1

--enter
--endenter

#vuprog DynPipVU1D

    ResetClipFlags{ }
    LoadTyraStaticData{ gifSetTag }
    MatrixLoad{ mvp, VU1_MVP_MATRIX_ADDR, vi00 }
    LoadTyraTags{ lodGifTag, testsTag, VU1_LOD_ADDR, VU1_Z_TESTS_ADDR }

begin:
    xtop buffer
    LoadTyraPrimTag{ primTag, buffer }

    ilw.w   vertexCount,        0(buffer)
    iaddiu  vertexDataFrom,     buffer,         VU1_DYNPIP_VERT_DATA_ADDR

    iadd    normalDataFrom,     vertexDataFrom,    vertexCount
    iadd    normalDataFrom,     normalDataFrom, vertexCount

    iadd    kickAddress,        normalDataFrom, vertexCount
    iadd    destAddress,        kickAddress,    vertexCount
    iadd    kickAddress,        kickAddress,    vertexCount

    StoreTyraGifTags{ gifSetTag, lodGifTag, primTag, testsTag, destAddress }
    LoadTyraLerpValue{ interp, VU1_OPTIONS_ADDR }
    LoadTyraScaleValue{ scale, buffer }

    ;--- Loop
    iadd vertexCounter, buffer, vertexCount
vertexLoop:

        iadd    vertexDataTo,   vertexDataFrom, vertexCount
        iadd    normalDataTo,   normalDataFrom, vertexCount

        ;--- Vertex 1 - from-to -> lerp
        lq      vertex1From,    (vertexDataFrom)
        lq      vertex1To,      (vertexDataTo)
        Lerp{ vertex1, vertex1From, vertex1To, interp }

        lq.xyz  normal1From,    (normalDataFrom)
        lq.xyz  normal1To,      (normalDataTo) 
        LerpXYZ{ normal1, normal1From, normal1To, interp }

        ;--- Vertex 1 - Calculate
        MatrixMultiplyVertex{ vertex1, mvp, vertex1 }
        PerformClipCheck{ vertex1, destAddress, XYZ2_STORE_OFFSET }
        VertexPersCorr{ vertex1, vertex1 }
        ScaleVertexToGSFormat{ scale, vertex1 }
        LoadTyraDirectionalLights{ lightMatrix, lightDirections, lightColors, ambientColor, VU1_LIGHTS_DIRS_ADDR, VU1_LIGHTS_COLORS_ADDR, VU1_LIGHTS_MATRIX_ADDR }
        CalculateTyraDirectionalLights{ outputColor1, normal1, lightDirections, lightColors, lightMatrix, ambientColor }
        FixColor{ outputColor1 }

        ;--- Vertex 1 - Store 
        sq      outputColor1,   RGBA_STORE_OFFSET(destAddress)
        sq.xyz  vertex1,        XYZ2_STORE_OFFSET(destAddress)

        ; ---------------------------------------

        ;--- Vertex 2 - from-to -> lerp
        lq      vertex2From,    1(vertexDataFrom)
        lq      vertex2To,      1(vertexDataTo)
        Lerp{ vertex2, vertex2From, vertex2To, interp }

        lq.xyz  normal2From,    1(normalDataFrom) 
        lq.xyz  normal2To,      1(normalDataTo) 
        LerpXYZ{ normal2, normal2From, normal2To, interp }

        ;--- Vertex 2 - Calculate
        MatrixMultiplyVertex{ vertex2, mvp, vertex2 }
        PerformClipCheck{ vertex2, destAddress, XYZ2_STORE_OFFSET }
        VertexPersCorr{ vertex2, vertex2 }
        ScaleVertexToGSFormat{ scale, vertex2 }
        LoadTyraDirectionalLights{ lightMatrix, lightDirections, lightColors, ambientColor, VU1_LIGHTS_DIRS_ADDR, VU1_LIGHTS_COLORS_ADDR, VU1_LIGHTS_MATRIX_ADDR }
        CalculateTyraDirectionalLights{ outputColor2, normal2, lightDirections, lightColors, lightMatrix, ambientColor }
        FixColor{ outputColor2 }

        ;--- Vertex 2 - Store
        sq      outputColor2,   RGBA_STORE_OFFSET+2(destAddress)
        sq.xyz  vertex2,        XYZ2_STORE_OFFSET+2(destAddress)

        ; ---------------------------------------

        ;--- Vertex 3 - from-to -> lerp
        lq      vertex3From,    2(vertexDataFrom)
        lq      vertex3To,      2(vertexDataTo)
        Lerp{ vertex3, vertex3From, vertex3To, interp }

        lq.xyz  normal3From,    2(normalDataFrom) 
        lq.xyz  normal3To,      2(normalDataTo) 
        LerpXYZ{ normal3, normal3From, normal3To, interp }

        ;--- Vertex 3 - Calculate
        MatrixMultiplyVertex{ vertex3, mvp, vertex3 }
        PerformClipCheck{ vertex3, destAddress, XYZ2_STORE_OFFSET }
        VertexPersCorr{ vertex3, vertex3 }
        ScaleVertexToGSFormat{ scale, vertex3 }
        LoadTyraDirectionalLights{ lightMatrix, lightDirections, lightColors, ambientColor, VU1_LIGHTS_DIRS_ADDR, VU1_LIGHTS_COLORS_ADDR, VU1_LIGHTS_MATRIX_ADDR }
        CalculateTyraDirectionalLights{ outputColor3, normal3, lightDirections, lightColors, lightMatrix, ambientColor }
        FixColor{ outputColor3 }

        ;--- Vertex 3 - Store
        sq      outputColor3,   RGBA_STORE_OFFSET+4(destAddress)
        sq.xyz  vertex3,        XYZ2_STORE_OFFSET+4(destAddress)

        ;-------------------------------

        iaddiu  vertexDataFrom, vertexDataFrom, 3
        iaddiu  normalDataFrom, normalDataFrom, 3  
        iaddiu  destAddress,    destAddress,    6

        ;--- Fix loop
        iaddi   vertexCounter,  vertexCounter,  -3  ; decrement the loop counter 
        ibne    vertexCounter,  buffer, vertexLoop  ; and repeat if needed

    xgkick kickAddress ; dispatch to the GS rasterizer.

--barrier ; Why the hell I must add barrier AFTER XGKICK? VCL does not adds E bit without it...
--cont

    b   begin

#endvuprog

--exit
--endexit
