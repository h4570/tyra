; ______       ____   ___
;   |     \/   ____| |___|
;   |     |   |   \  |   |
;---------------------------------------------------------------
; Copyright 2020 - 2022, tyra - https://github.com/h4570/tyra
; Licenced under Apache License 2.0
; Sandro Sobczy≈Ñski <sandro.sobczynski@gmail.com>
;
;---------------------------------------------------------------
; Triangle list
; AsIs = NO TRANSFORM
; Texture, directional lights
;---------------------------------------------------------------

.syntax new
.name StdpipVU1As_Is_TD
.vu
.init_vf_all
.init_vi_all

#include "src/renderer/core/paths/path1/programs/vcl_sml.i"
#include "src/renderer/core/paths/path1/programs/tyra_macros.i"
#include "inc/renderer/3d/pipeline/std/core/path1/programs/stdpip_vu1_shared_defines.h"

#define STQ_STORE_OFFSET    0
#define RGBA_STORE_OFFSET   1
#define XYZ2_STORE_OFFSET   2

--enter
--endenter

#vuprog StdpipVU1AsIsTD

    LoadTyraStaticData{ gifSetTag }
    LoadTyraDirectionalLights{ lightMatrix, lightDirections, lightColors, ambientColor, VU1_LIGHTS_DIRS_ADDR, VU1_LIGHTS_COLORS_ADDR, VU1_LIGHTS_MATRIX_ADDR }
    LoadTyraTags{ lodGifTag, texBufferClutGifTag, VU1_LOD_ADDR, VU1_CLUT_ADDR }

begin:
    xtop buffer
    LoadTyraBufferTags{ scale, primTag, buffer }

    iaddiu  vertexData,         buffer,         VU1_VERT_DATA_ADDR
    ilw.w   vertexCount,        0(buffer)
    iadd    stqData,            vertexData,     vertexCount
    iadd    normalData,         stqData,        vertexCount
    iadd    kickAddress,        normalData,     vertexCount
    iadd    destAddress,        normalData,     vertexCount

    StoreTyraGifTags{ gifSetTag, lodGifTag, texBufferClutGifTag, primTag, destAddress }

    ;--- Loop
    iadd vertexCounter, buffer, vertexCount
vertexLoop:
        ;--- Load vertex1
        lq      vertex1,  (vertexData)
        lq      stq1,     (stqData)
        lq.xyz  normal1,  (normalData) 

        ;--- Load vertex2
        lq      vertex2,  1(vertexData)
        lq      stq2,     1(stqData)
        lq.xyz  normal2,  1(normalData) 

        ;--- Load vertex3
        lq      vertex3,  2(vertexData)
        lq      stq3,     2(stqData)
        lq.xyz  normal3,  2(normalData) 

        ;--- Calculate vertex1
        ScaleVertexToGSFormat{ scale, vertex1 }
        div q,  vf00[w],    vertex1[w]
        PerformTexturePerspectiveCorrection{ outputStq1, stq1 }
        CalculateTyraDirectionalLights{ outputColor1, normal1, lightDirections, lightColors, lightMatrix, ambientColor }
        FixColor{ outputColor1 }

        ;--- Calculate vertex2
        ScaleVertexToGSFormat{ scale, vertex2 }
        div q,  vf00[w],    vertex2[w]
        PerformTexturePerspectiveCorrection{ outputStq2, stq2 }
        CalculateTyraDirectionalLights{ outputColor2, normal2, lightDirections, lightColors, lightMatrix, ambientColor }
        FixColor{ outputColor2 }

        ;--- Calculate vertex3
        ScaleVertexToGSFormat{ scale, vertex3 }
        div q,  vf00[w],    vertex3[w]
        PerformTexturePerspectiveCorrection{ outputStq3, stq3 }
        CalculateTyraDirectionalLights{ outputColor3, normal3, lightDirections, lightColors, lightMatrix, ambientColor }
        FixColor{ outputColor3 }

        ;--- Store vertex1
        sq      outputStq1,     STQ_STORE_OFFSET(destAddress)
        sq      outputColor1,   RGBA_STORE_OFFSET(destAddress)
        sq.xyz  vertex1,        XYZ2_STORE_OFFSET(destAddress)

        ;--- Store vertex2
        sq      outputStq2,     STQ_STORE_OFFSET+3(destAddress)
        sq      outputColor2,   RGBA_STORE_OFFSET+3(destAddress)
        sq.xyz  vertex2,        XYZ2_STORE_OFFSET+3(destAddress)

        ;--- Store vertex3
        sq      outputStq3,     STQ_STORE_OFFSET+6(destAddress)
        sq      outputColor3,   RGBA_STORE_OFFSET+6(destAddress)
        sq.xyz  vertex3,        XYZ2_STORE_OFFSET+6(destAddress)

        ;-------------------------------

        iaddiu  vertexData,     vertexData,     3                         
        iaddiu  stqData,        stqData,        3  
        iaddiu  normalData,     normalData,     3  
        iaddiu  destAddress,    destAddress,    9

        ;--- Fix loop
        iaddi   vertexCounter,  vertexCounter,  -3  ; decrement the loop counter 
        ibne    vertexCounter,  buffer, vertexLoop  ; and repeat if needed

    xgkick kickAddress ; dispatch to the GS rasterizer.

--barrier ; Why the hell I must add barrier AFTER XGKICK? VCL does not adds E bit without it...
--cont

    b   begin

#endvuprog

--exit
--endexit
